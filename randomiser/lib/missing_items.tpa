ACTION_IF Tutu = 1 AND !FILE_EXISTS_IN_GAME _leat06.itm BEGIN
  COPY_EXISTING leat06.itm "override/_leat06.itm"
END
ACTION_IF Tutu = 1 AND !FILE_EXISTS_IN_GAME _sper03.itm BEGIN
  COPY_EXISTING sper03.itm "override/_sper03.itm"
END

COPY "randomiser/copy/flbhcryp.itm" override

ACTION_IF (BG1 = 0 OR BGT = 1) AND VARIABLE_IS_SET $RandOptions(flrc520) BEGIN

<<<<<<<< ...randomiser/fl-inlined/crypt.baf
IF
  Clicked([ANYONE])
  Range(LastTrigger,12)
  OR(2)
  !PartyHasItem("%ward%")
  !Global("KangaxxQuest","GLOBAL",1)
  Global("UsedCrypt","%area%",0)
THEN RESPONSE #100
  DisplayStringHead(Myself,@1500)
END
>>>>>>>>

  COPY_EXISTING crypt526.bcs override
                crypt202.bcs override
    DECOMPILE_BCS_TO_BAF
      //REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~\( +Global(\"UsedCrypt\",\"AR0202\",0)\)~ ~\1 PartyHasItem("flrward2")~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~^\( +Global(\"UsedCrypt\",\"AR\(0526\|0202\)\",0)\)~ ~\1 PartyHasItem("flrward1") Global("KangaxxQuest","GLOBAL",1)~
      REPLACE 35858 @1501
      //REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~\( +SetGlobal(\"UsedCrypt\",\"AR0202\",1)\)~ ~\1 TakePartyItem("flrward2")~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~\(SetGlobal(\"UsedCrypt\",\"AR\(0526\|0202\)\",1)\)~ ~\1 TakePartyItem("flrward1")~
    COMPILE_BAF_TO_BCS
  //UNLESS "flrward[12]"

  OUTER_SPRINT ward flrward1
  OUTER_SPRINT area ar0526
  EXTEND_TOP crypt526.bcs ~...randomiser/fl-inlined/crypt.baf~ EVAL
  //OUTER_SPRINT ward flrward2
  OUTER_SPRINT area ar0202
  EXTEND_TOP crypt202.bcs ~...randomiser/fl-inlined/crypt.baf~ EVAL

  COPY "randomiser/copy/ward.itm" "override/flrward1.itm"
    SAY 0x8 @1503
    SAY 0xc @1503
    WRITE_ASCII 0x3a flrward1
    SAY 0x54 @1502
    SAY 0x50 @1504

  COPY "randomiser/copy/flrward1.bam" override
  /*
  COPY "randomiser/copy/ward.itm" "override/flrward2.itm"
    SAY 0x8 @1503
    SAY 0xc @1503
    SAY 0x50 @1504
    SAY 0x54 @1502*/
END

ACTION_IF (BG1 = 0 OR BGT = 1) AND VARIABLE_IS_SET $RandOptions(flrc550) BEGIN

  COPY "randomiser/copy/flrbar.itm" override
    SAY 0x8 @1510
    SAY 0xc @1511
    WRITE_ASCII 0x3a flribar #8
    SAY 0x50 @1512
    SAY 0x54 @1513
    WRITE_ASCII 0x58 flrcbar #8

  COPY "randomiser/copy/flribar.bam" override
       "randomiser/copy/flrcbar.bam" override
END

//Stores do not carry excessively large stacks of items, because I say so
ACTION_IF BG1 BEGIN
  ACTION_FOR_EACH file IN "%TUTU_VAR%sto4803.sto" "%TUTU_VAR%friend.sto" "%TUTU_VAR%ulgoth.sto" BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME "%file%" BEGIN
      COPY_EXISTING "%file%" override
        PATCH_IF SOURCE_SIZE > 0x9b BEGIN
          LPF change_store_stock STR_VAR item = EVAL "%TUTU_VAR%shld06" END
        END
      BUT_ONLY
    END
  END
END

ACTION_IF GAME_IS ~bgee eet~ BEGIN
  COPY_EXISTING sto0703.sto override
    LPF change_store_stock STR_VAR item = "wand[0-9]+" END
    LPF change_store_stock STR_VAR item = "scrl5[pmgijk]" END
  BUT_ONLY
END

/*
 * Jamis has the good weapon equipped in the off-hand; swap
 */
ACTION_IF FILE_EXISTS_IN_GAME amtgen01.cre AND (BG1 = 0 OR BGT = 1) BEGIN
  COPY_EXISTING amtgen01.cre override
    PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
      READ_LONG 0x2b8 itm_slt
      READ_LONG 0x2bc itm_off
      READ_SSHORT itm_slt + 0x2 * 2 offhand ELSE "-1"
      PATCH_IF offhand >= 0 BEGIN
        READ_ASCII itm_off + 0x14 * offhand offweapon ELSE blank
      END
      PATCH_IF "%offweapon%" STRING_EQUAL_CASE "sw1h77" BEGIN // The Answerer +4
        FOR (i = 0; i < 5; ++i) BEGIN
          READ_SSHORT itm_slt + 0x2 * 9 + 0x2 * i mainhand ELSE "-1"
          PATCH_IF mainhand >= 0 BEGIN
            READ_ASCII itm_off + 0x14 * mainhand mainweapon ELSE blank
            PATCH_IF "%mainweapon%" STRING_EQUAL_CASE "sw1h73" BEGIN // Long Sword +3
              WRITE_SHORT itm_slt + 0x2 * 2 mainhand
              WRITE_SHORT itm_slt + 0x2 * 9 + 0x2 * i offhand
              i = 5
            END
          END
        END
      END
    END
  BUT_ONLY
END
